// Authors: Charles duMars
// All content Â© 2017 DigiPen (USA) Corporation, all rights reserved.
class FearManager : ZilchComponent
{
    var FearBar : Cog = null;
    
    var BatteryBar : Cog = null;
    
    var BatteryLife : Real = 60;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, Events.CollisionStarted, this.OnCollisionStarted);
        this.FearBar = this.Space.LevelSettings.HUDCreator.HUDSpace.LevelSettings.HUDReferences.FearBar;
        this.BatteryBar = this.Space.LevelSettings.HUDCreator.HUDSpace.LevelSettings.HUDReferences.BatteryLife;
        this.BatteryLife = this.GameSession.GameVars.BatteryLife;
    }

    function OnLogicUpdate(event : UpdateEvent)
    {
        this.BatteryBar.Bar.Decrease(event.Dt/this.BatteryLife);
        this.GameSession.GameVars.BatteryLife -= (event.Dt/this.BatteryLife);
        if(this.BatteryBar.Transform.Scale.X == 0.0001 || this.FearBar.Transform.Scale.Y >= this.FearBar.Bar.MaxSize)
        {
            this.Space.LoadLevel(Level.LoseScreen);
        }
    }
    function OnCollisionStarted(event : CollisionEvent)
    {
        if(event.OtherObject.MonsterAI != null)
        {
            this.Spook(1);
        }
    }
    function Spook(amnt : Real)
    {
        this.FearBar.Bar.Increase(amnt);
        var max = this.FearBar.Bar.MaxSize;
        var scale = this.FearBar.Transform.Scale.Y;
        if(scale >= max*0.85)
        {
            if(this.FearBar.Sprite.Color != Real4(0.929412, 0.109804, 0, 1))
            {
                this.FearBar.Sprite.Color = Real4(0.929412, 0.109804, 0, 1);
            }
        }
        else
        {
            if(scale >= max*0.5)
            {
                this.FearBar.Sprite.Color = Real4(0.929412, 0.109804, 0.929412 * ((0.85 - (scale/max))/0.35), 1);
            }
            else if(this.FearBar.Transform.Scale.Y >= max*0.15)
            {
                this.FearBar.Sprite.Color = Real4(0.929412 * (( (scale/max) - 0.15)/0.35), 0.109804, 0.929412, 1);
            }
            else
            {
                this.FearBar.Sprite.Color = Real4(0, 0.109804, 0.929412, 1);
            }
        }
    }
}
