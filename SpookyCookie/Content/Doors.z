// Authors: Brenan Bundy
// All content Â© 2017 DigiPen (USA) Corporation, all rights reserved.
class Doors : ZilchComponent
{
    [Property]
    var NotLocked : Boolean = false;
    
    var Locked : Boolean = true;
    var HUD : Space = null;
    var Linger : Real = -1;
    
    [Property]
    var LevelToLoad : Level;
    [Property]
    var HallwayPos : Real3;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(this.Owner, Events.CollisionStarted, this.OnCollisionStarted);
        this.HUD = this.Space.LevelSettings.HUDCreator.HUDSpace;
        if(this.NotLocked)
            this.Locked = false;
    }
    function OnCollisionStarted(event : CollisionEvent)
    {
        if(!this.Locked && event.OtherObject.PlayerMovement != null)
        {
            event.OtherObject.PlayerMovement.HitDoor = true;
            this.CheckLockState();
        }
        else if(event.OtherObject.PlayerMovement != null)
        {
            if(this.Linger == -1)
            {
                this.HUD.LevelSettings.HUDReferences.LockedText.Fade.StartFade = true;
                this.Linger = 0;
            }
        }
    }
    function OnLogicUpdate(event : UpdateEvent)
    {
        if(this.Linger >= 0)
        {
            this.Linger += event.Dt;
            if(this.Linger > 1.25)
            {
                this.Linger = -1;
                this.HUD.LevelSettings.HUDReferences.LockedText.Fade.StartFade = true;
            }
        }
        else if(this.Linger <= -2)
        {
            this.Linger -= event.Dt;
            if(this.Linger < 3)
            {
                this.GameSession.GameVars.HallwayPos = this.HallwayPos;
                this.Space.LoadLevel(this.LevelToLoad);
            }
        }
        if(!this.Owner.Sprite.AnimationActive)
            return;
        
        if(this.Owner.Sprite.CurrentFrame == 4)
        {
            this.Owner.Sprite.SpriteSource = SpriteSource.Open_Door;
            this.Linger = -2;
        }
    }
    function CheckLockState()
    {
        if((this.NotLocked) || (!this.Locked && this.Owner.Sprite.SpriteSource != SpriteSource.Open_Door))
        {
            this.Owner.Sprite.AnimationActive = true;
        }
    }
}
